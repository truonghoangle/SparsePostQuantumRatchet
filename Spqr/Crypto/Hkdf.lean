/-
Copyright (c) 2026 The Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE-APACHE.
Authors: Oliver Butterley
-/
module

public import SrcTranslated.Funs
public import Spqr.Auxiliary.Aeneas.Scalar
public import Spqr.Crypto.RFC5869

/-!
# HKDF-SHA256

Definitions, not spec theorems: `RFC5869` formalises HKDF over `UInt8`, and this file instantiates
it at SHA-256 and re-expresses it over Aeneas bytes, so that specs can refer to `hkdf` directly.

- `HMAC_SHA256` is the one assumed primitive: an opaque 32-octet tag.
- `SHA256` packages it as an RFC 5869 hash parameter.
- `hkdf` is RFC 5869 §2 at SHA-256, over Aeneas byte lists.
-/

@[expose] public section

-- TODO: this should be specific for this use case or upstreamed.
private instance List.instInhabitedSubtypeEqNatLength (ty : Type) [Inhabited ty] (n : ℕ) :
    Inhabited { l : List ty // l.length = n } :=
  ⟨List.replicate n (default : ty), List.length_replicate⟩

open Aeneas Aeneas.Std Result Aeneas.Std.WP
namespace crypto

/-- HMAC-SHA256: a 32-octet authentication tag. -/
opaque HMAC_SHA256 (key data : List UInt8) : { l : List UInt8 // l.length = 32 }

/-- SHA-256 as an RFC 5869 hash parameter: `HashLen = 32`. -/
def SHA256 : HKDF.HashFunction where
  HashLen := 32
  HashLen_pos := by omega
  HMAC key data := (HMAC_SHA256 key data).val
  HMAC_length key data := (HMAC_SHA256 key data).property

/-- HKDF-SHA256 with an explicit salt: `L` octets of output keying material derived from `salt`,
`IKM` and `info`. -/
def hkdf (salt IKM info : List U8) (L : Nat) : List U8 :=
  (HKDF.ExtractAndExpand SHA256 (some (salt.map U8.toUInt8)) (IKM.map U8.toUInt8)
    (info.map U8.toUInt8) L).map U8.ofUInt8

/-- HKDF emits exactly `L` octets. -/
@[simp, scalar_tac_simps, grind =]
theorem hkdf_length (salt IKM info : List U8) (L : Nat) : (hkdf salt IKM info L).length = L := by
  simp [hkdf, HKDF.ExtractAndExpand_length]

/-- HKDF info label: `"Signal PQ Ratchet V1 Chain Next"` as `List U8`. -/
def chainNextLabel : List U8 :=
  [83#u8, 105#u8, 103#u8, 110#u8, 97#u8, 108#u8, 32#u8,
   80#u8, 81#u8, 32#u8, 82#u8, 97#u8, 116#u8, 99#u8,
   104#u8, 101#u8, 116#u8, 32#u8, 86#u8, 49#u8, 32#u8,
   67#u8, 104#u8, 97#u8, 105#u8, 110#u8, 32#u8, 78#u8,
   101#u8, 120#u8, 116#u8]

/-- Zero salt: 32 zero bytes. -/
def zeroSalt32 : List U8 := List.replicate 32 0#u8

/-- HKDF info: `ctr1.to_be_bytes() ++ chainNextLabel`. -/
def nextKeyInfo (ctr1 : U32) : List U8 :=
  (ctr1.bv.toBEBytes.map (fun b : Byte => U8.ofUInt8 ⟨b⟩)) ++ chainNextLabel

/-- Full 64-byte HKDF output from `ikm` and incremented counter `ctr1`. -/
noncomputable def nextKeyHkdfOutput (ikm : List U8) (ctr1 : U32) : List U8 :=
  hkdf zeroSalt32 ikm (nextKeyInfo ctr1) 64

/-- HKDF info label: `"Signal PQ Ratchet V1 Chain  Start"` (33 bytes) as `List U8`. -/
def chainStartLabel : List U8 :=
  [83#u8, 105#u8, 103#u8, 110#u8, 97#u8, 108#u8, 32#u8,
   80#u8, 81#u8, 32#u8, 82#u8, 97#u8, 116#u8, 99#u8,
   104#u8, 101#u8, 116#u8, 32#u8, 86#u8, 49#u8, 32#u8,
   67#u8, 104#u8, 97#u8, 105#u8, 110#u8, 32#u8,
   32#u8, 83#u8, 116#u8, 97#u8, 114#u8, 116#u8]

/-- The 96-byte HKDF-SHA256 output derived from `initial_key` with zero salt and
    the chain-start info label. -/
noncomputable def newHkdfOutput (initial_key : List U8) : List U8 :=
  crypto.hkdf crypto.zeroSalt32 initial_key chainStartLabel 96

/-- HKDF info label: `"Signal PQ Ratchet V1 Chain Add Epoch"` (36 bytes) as `List U8`. -/
def chainAddEpochLabel : List U8 :=
  [83#u8, 105#u8, 103#u8, 110#u8, 97#u8, 108#u8, 32#u8,
   80#u8, 81#u8, 32#u8, 82#u8, 97#u8, 116#u8, 99#u8,
   104#u8, 101#u8, 116#u8, 32#u8, 86#u8, 49#u8, 32#u8,
   67#u8, 104#u8, 97#u8, 105#u8, 110#u8, 32#u8,
   65#u8, 100#u8, 100#u8, 32#u8, 69#u8, 112#u8, 111#u8, 99#u8, 104#u8]

/-- The 96-byte HKDF-SHA256 output derived from `next_root` and `epoch_secret` with
    the chain-add-epoch info label. -/
noncomputable def addEpochHkdfOutput (next_root secret : List U8) : List U8 :=
  crypto.hkdf next_root secret chainAddEpochLabel 96

end crypto
